title: variables
---
<<set $IsSkip = false>>


<<set $DeskEvidence = false>> //안쓰는 거
<<set $BedBloodCleanOrNot = false>> 


<<set $PhotoDelete = 0>> //살인증거
<<set $BloodTissue = 0>> 
<<set $FingerInCakeBox = 0>>
<<set $FloorHammer = 0>>
<<set $StalkingPicture = 0>>
<<set $BloodHandprint = 0>>
<<set $SmellOfBlood = 0>>
<<set $CigaretteInBathroom = 0>>
<<set $BloodOnCloth = 0>>
<<set $BloodOnRug = 0>>

<<set $LighterAndCigarette = false>>
<<set $MentalHospitalCall = false>>
<<set $HammerInTrashCan = 0>>
<<set $BloodOnClothInCloset = false>>

<<set $BathRoomKey = false>> 
<<set $DresserKey = false>>
<<set $CheckTheMajor = false>>

<<set $ClueToTheTruth = 0>> //진실증거
<<set $OhHanaTalk = 0>>
<<set $ShimJaehwanTalk = 0>>

<<set $Ring1 = 0>>
<<set $Ring2 = 0>>
===
title: StalkingPhoto1
---
이건... 아무래도 내가 재환이를 몰래 찍은 거겠지.
지울까?
-> 당연하지.
	사진을 삭제했다.
	<<set $PhotoDelete = $PhotoDelete+1>>
	<<photoDelete Photo1 Photo1>>

-> 아니야.
	그래, 그냥 두자.
===
title: StalkingPhoto2
---
이건... 아무래도 내가 재환이를 몰래 찍은 거겠지.
지울까?
-> 당연하지.
	사진을 삭제했다.
	<<set $PhotoDelete = $PhotoDelete+1>>
	<<photoDelete Photo2 Photo2>>

-> 아니야.
	그래, 그냥 두자.
===
title: StalkingPhoto3
---
이건... 아무래도 내가 재환이를 몰래 찍은 거겠지.
지울까?
-> 당연하지.
	사진을 삭제했다.
	<<set $PhotoDelete = $PhotoDelete+1>>
	<<photoDelete Photo3 Photo3>>

-> 아니야.
	그래, 그냥 두자.
===
title: StalkingPhoto4
---
이건... 아무래도 내가 재환이를 몰래 찍은 거겠지.
지울까?
-> 당연하지.
	사진을 삭제했다.
	<<set $PhotoDelete = $PhotoDelete+1>>
	<<photoDelete Photo4 Photo4>>

-> 아니야.
	그래, 그냥 두자.
===

title: Diagnosis
---
병원 진단 기록인듯 하다.
진료 날짜는 11월 3일이다.
===
title: OhHana
---
...어라? 메신저함이 깔끔하게 비워져 있다.
지운 이유가 있을까?
<<set $OhHanaTalk = 1>>
===
title: ShimJaehwan
---
...협박 문자다.
아마 살인 사건이 있었던 날 보낸 거겠지.
심재환이 이 사건을 목격이라도 한 걸까?
<<set $ShimJaehwanTalk = 1>>
===
title: JoyApp
---
비밀번호가 걸려있는 어플이다.
대체 뭐하는 어플이지?
...비밀번호는 기억나지 않는다.
나: 나중에 열어봐도 되겠지. 지금은 건들 필요 없겠어.
===

title: Blood
---
<color=red>피 묻은 자국</color>이 있다.
-> 지워야 해.
	자국을 깨끗이 지웠다.
	<<set $BedBloodCleanOrNot = true>>

-> 그대로 둬도 괜찮을 것 같아.
-> 핏자국을 확인해봐야겠어.
	이건... 피가 튀었다기보단 천에 흐르는 피가 닦인 것 같다.
	어떤 상황이었던 거지?
===

title: Desk
---
공부할 때 쓰는 책상이다.
위에는 각종 교재와 노트가 늘어져있다.
앞에는 책장이 자리잡고 있다.
-> 책장엔 뭐가 있지?
	어학 시험 준비를 위한 문제집, 프로그래밍 언어 교습서 등이 있다.
	자주 읽는 경제학 책이나 자기계발서도 꽂혀있다.
	<<if $DeskEvidence == false>>
		한 책의 표면에 <color=red>붉은 손자국</color>이 있다.
		-> 당장 지워야 한다.
			자국을 깨끗이 지웠다.
			<<set $DeskEvidence = true>>
		-> 이정도면 경찰도 보지 못할 것이다.
			대충 냅뒀다.
		-> 이런게 어떻게 생긴 거지?
			아무리 봐도 어쩌다 생긴 것 같진 않다.
			누가 일부러 만들어둔 걸까?
	<<endif>>

-> 교재들만 멀쩡하면 그만이다.

===

title: DresserDrawerOpen
---
<<if $KitchenCabinetKey == false>>
	<<ringAppear Key3>>
	화장대 서랍을 열자 못 보던 열쇠가 들어있다.
	잠긴 문이 또 어디 있더라?
	<<set $KitchenCabinetKey = true>>
	<<objectDelete Key3 Key3>>
	<<mentalHospitalActive MentalHospital>>
	<<setNumber 2>>
	<<playSound SoundManager 7>>
	<<wait 1>>
	<<stopSound SoundManager>>
	<<setNumber 1>>
	<<objectDelete MentalHospital MentalHospital>>
<<else>>
	신경 쓰이는 것은 없다.
<<endif>>
===
title: DresserDrawerLocked
---
<<playSound SoundManager 42>>
화장대 서랍이다. 잠겨있다.
===
title: MentalHospitalCall
---
<<stopSound SoundManager>>
<<wait 1>>
??: ......에요?
??: 어? 받으셨네.
??: 안녕하세요, 환자분. 성하 정신건강의학과입니다.
??: 제가 예약 시간을 착각해서 잘못 전화드렸네요.
나: 네... 네? 무슨 말씀이시죠?
??: 아, 예약을 오늘로 알고 안 오신줄 알았어요. 다음에 다시 연락드릴게요.
나: 아, 네......
<<playSound SoundManager 31>>
<<objectDelete MentalHospital MentalHospital>>
<<wait 0.5>>
<<stopSound SoundManager>>
......방금 뭐지?
잘못 걸려온건가...... 어쨌든 조사나 더 해보자.
<<setNumber 1>>
<<set $MentalHospitalCall = true>>
===
title: KitchenCabinetLocked
---
<<playSound SoundManager 42>>
부엌 찬장이다. 잠겨있다.
===
title: Drug
---
부엌 찬장 안에 수면제가 들어 있다. 
이건 약효가 강해 의사가 처방 없이 살 수 없다.
나는 이 사실을 잘 알고 있다.
내가 이 수면제를 처방받은 이유도 알고 있다.
내가 이 수면제를 처방받은 이유는…
-> 내가 누군가를 스토킹했기 때문에.
	어쩌면 정신병원에 찾아갔을지도 모르지.
-> 누군가가 나를 스토킹했기 때문에.
	......응? 그 반대 아닌가?
-> 망상 때문에 잠에 들지 못해서. <<if $MentalHospitalCall == true>>
	<<jump Ending2Enter>>
===

title: KitchenFloor
---
<<if $FloorHammer == 0>> //망치가 있으면
	아까 내가 발견한 망치이다. 철 부분이 온통 피 범벅이다.
	어떻게 처리할까?
	-> 쓰레기통에 쑤셔 넣는다.
		<<playSound SoundManager 25>>
		<<wait 1>>
		<<stopSound SoundManager>>
		쓰레기는 쓰레기통에!
		쓰레기통에 쑤셔 넣었다.
		<<set $FloorHammer = 1>> //망치 숨김
		<<set $HammerInTrashCan = 1>> //쓰레기통에 망치가 생김
		<<objectDelete KitchenFloor KitchenFloor>>
	-> 침대 밑에 숨긴다.
		침대 밑에 숨기면 아무도 찾지 못할 것이다.
		침대 밑에 숨기려고 이불을 들췄는데, 무언가 반짝이는 것이 보인다. 
		<<ringAppear Key1>>
		<<playSound SoundManager 26>>
		<<wait 0.5>>
		화장실 문 열쇠를 발견했다.
		<<objectDelete Key1 Key1>>
		<<set $FloorHammer = 1>> //망치 숨김
		<<set $BathRoomKey = true>> //화장실 열쇠 발견
		<<objectDelete KitchenFloor KitchenFloor>>
		
	-> 이대로 둔다.
		아직은 처리 방법이 떠오르지 않는다. 일단 이대로 내버려 두자.
<<else>> //망치가 없으면
	신경쓰이는 것은 없다.
<<endif>>
===

title: Window
---
<<if $BloodHandprint == 0>> //손자국이 있으면
	창문에 손모양의 핏자국이 있다.
	일단 닦아낼까?
	-> 당연하지!
		<<playSound SoundManager 17>>
		생각보다 높이 있어 닦기가 어려웠다.
		침대 위에 올라가 핏자국을 닦아냈다.
		<<set $BloodHandprint = 1>> //손자국 지워짐
		<<objectDelete HandPrint HandPrint>>
	-> 굳이?
		그래. 그냥 두자. 어차피 블라인드 내리면 안 보이겠지.
<<else>>
	하늘이 온통 흩뿌옇다.
	비가 올지도 모르겠다.
<<endif>>
===
title: StalkingPicture
---
<<if $StalkingPicture == 0>>
	스토킹의 흔적이다. 심재환의 사진이 잔뜩 붙어있다.
	어떻게 할까?
	-> 떼어내어 정리한 후 옷장 속 옷 주머니 안에 숨긴다.
		<<playSound SoundManager 22>>
		<<objectDelete StalkingPicture StalkingPicture>>
		<<wait 1>>
		옷장 속 옷 주머니 안에 사진을 넣으려다 무언가를 발견했다. 
		라이터와 담배가 들어있다. 
		<<set $StalkingPicture = 1>> //스토킹 사진 옷 속에 숨김
		<<set $LighterAndCigarette = true>> //라이터와 담배 발견
		이걸로 무엇을 할까?
		-> 라이터로 사진을 태운다.
			<<playSound SoundManager 23>>
			<<wait 0.5>>
			라이터를 켠 뒤 사진을 라이터에 갖다 댔다.
			타는 냄새와 함께 사진이 형체도 남기지 않고 사라졌다.
			라이터를 다시 켜보려 하니 그게 마지막 기름이었는지 더 이상 켜지지 않는다.
			<<set $LighterAndCigarette = false>> //라이터 망가짐
		-> 일단 둘 다 챙긴다.
			언제 쓸 지 모르니 둘 다 주머니에 챙겨두었다.
		-> 담배는 백해무익! 둘 다 버린다.
			기억을 잃어버린 김에 담배도 끊기로 결심했다. 
			담배와 라이터를 모두 쓰레기통에 버렸다. 
			건강하게 살자!
			<<set $LighterAndCigarette = false>> //라이터와 담배 모두 버림
	-> 떼어내어 책 속에 숨긴다.
		<<playSound SoundManager 22>>
		<<wait 1>>
		제일 두꺼운 책을 꺼내 그 안에 사진을 넣었다.
		<<set $StalkingPicture = 1>> //스토커 사진 책 속에 숨김
		<<objectDelete StalkingPicture StalkingPicture>>
	-> 경찰이 들어오면 우리만의 추억이라고 우긴다.
		그냥 이대로 두고 추억이라고 우겨야겠다. 추억은 맞잖아?
<<elseif $StalkingPicture == 1>>
	테이프 자국도 없이 깨끗하다.
<<endif>>
===
title: Rug
---

<<if $BloodOnRug == 0>>
	피가 흥건히 묻어 있는 러그다.
	닦을까?
	-> 예.
		<<playSound SoundManager 24>>
		열심히 문질러 지우자 약간의 자국은 남았지만 크게 눈에 띄지는 않는다.
		<<set $BloodOnRug = 1>>
		<<objectDelete Rug Rug>>
	-> 아니오.
		그냥 얼룩인 줄 알거야.
<<else>>		
	평범한 러그다.
<<endif>>
===
title: BookShelf
---
아무래도 전공 교재인 듯하다. 
<<playSound SoundManager 19>>
순서대로 표지에 <color=yellow>‘인지심리학, 자료구조, 데이터베이스 개론과 실습, 사회심리학의 이해’</color>라고 적혀있다.
<<if $CheckTheMajor == false>>
	그런데 내 전공이… 뭐였더라?
	-> 심리학과 학생이다.
		그렇다면 이 <color=yellow>‘자료구조’</color>와 <color=yellow>‘데이터베이스 개론과 실습’</color>의 주인은 내가 아니겠군.
		<<set $CheckTheMajor = true>>
	-> 컴퓨터공학과 학생이다.
		그렇다면 이 <color=yellow>‘인지심리학’</color>과 <color=yellow>‘사회심리학’</color>의 주인은 내가 아니겠군. 누가 주인일까?
		<<set $CheckTheMajor = true>>
		-> 심재환이 주인이다.
			흠, 과연 심재환의 책일까?
			<<set $ClueToTheTruth = 1>>
		-> 오하나가 주인이다.
			흠, 과연 오하나의 책일까?
			<<set $ClueToTheTruth = 1>>
		-> 이지연이 주인이다.
			지연이는… 나와 같은 과 아니었나?
	-> 심리학과와 컴퓨터공학과를 복수전공 중이다. 
		그렇다면 이 책들 모두 내 것이겠군.
		<<set $CheckTheMajor = true>>
	-> 좀 더 생각해봐야겠어. 
<<endif>>
===
title: BathRoom
---
<<playSound SoundManager 9>>
화장실 문을 열고 들어섰다. 
<<if $SmellOfBlood == 0>> //화장실 피냄새
	희미한 쇠 냄새가 감돈다.
	이 희미한 쇠 냄새는 아마도 피 냄새겠지…
	찜찜하다. 냄새를 어떻게 지울까?
	-> 방 창문을 열고 환기시킨다.
		<<playSound SoundManager 32>>
		<<wait 1>>
		창문과 문을 모두 열고 환기를 시작했다.
		큰 효과는 없었다.
		결국 창문을 다시 닫았다.
	-> 포기한다.
		냄새가 좀 나지만… 별 수 없다. 이대로 두자.
	
	-> 담배를 피워 냄새를 없앤다. <<if $LighterAndCigarette == true>>
		<<playSound SoundManager 23>>
		<<wait 1>>
		담배에 불을 붙였다. 
		독한 담배향 덕분에 냄새가 금방 지워진다. 
		라이터의 기름이 다 떨어졌는지 라이터가 더 이상 켜지지 않는다.
		<<set $LighterAndCigarette = false>>
		<<set $SmellOfBlood = 1>>
		<<set $CigaretteInBathroom = 1>>	
<<else>>
	이제 별다른 냄새가 나지 않는다.
<<endif>>
===
title: BathRoomLocked
---
<<playSound SoundManager 42>>
화장실 문이 잠겨있다.
따로 열쇠가 있는 모양이다.
===
title: Basin
---
<<if $BloodOnCloth == 0>>
	아무래도 쇠냄새의 원인을 찾은 것 같다.
	<<playSound SoundManager 20>>
	<<wait 1>>
	옷을 뒤적거리는데 주머니에서 무언가 나온다.
	<<ringAppear ring1>>
	<<playSound SoundManager 26>>
	이건... <color=\#74F0FF>반지</color>?
	주인을 알 수 없는 반지를 주웠다.
	<<set $Ring1 = 1>>
	은색에 검은 진주가 박힌 예쁜 반지다.
	<<objectDelete ring1 ring1>>
	피 묻은 옷은 어디에 숨겨둘까? 
	-> 옷장 안에 숨긴다.
		<<set $BloodOnClothInCloset = true>>
	-> 부엌 서랍 안에 숨긴다.
	-> 침대 밑에 숨긴다.
	<<wait 0.7>>
	<<set $BloodOnCloth = 1>>
	<<objectDelete BloodCloth BloodCloth>>
	<<objectDelete BloodCloth2 BloodCloth2>>
	옷을 숨기고 세면대로 돌아오자 무언가 반짝이는 것이 보인다. 
	<<playSound SoundManager 20>>
	<<ringAppear Key2>>
	화장대 서랍 열쇠를 발견했다.
	<<set $DresserKey = true>>
	<<wait 0.5>>
	<<objectDelete Key2 Key2>>
<<endif>>
===
title: BathRoomTrashCan
---
<<if $Ring2 == 0>>
	<<playSound SoundManager 20>>
	<<wait 0.5>>
	쓰레기통 속에서 무언가 반짝거린다.
	<<ringAppear ring22>>
	<<playSound SoundManager 26>>
	은색에 검은 진주가 박힌 예쁜 <color=\#74F0FF>반지</color>다.
	<<set $Ring2 = 1>>
	<<objectDelete ring22 ring22>>
<<else>>
	쓰레기가 가득 차있다.
<<endif>>
===
title: TrashCan
---
쓰레기통 안에는 쓰레기들이 가득하다.
<<if $HammerInTrashCan == 1>>
	아까 넣은 망치 손잡이도 살짝 보인다.
<<endif>>
<<if $BloodTissue == 0>>
	<<playSound SoundManager 20>>
	조금 뒤적거리자 <color=red>피 묻은 휴지</color>들이 나왔다.
	어떻게 하지?
	<<stopSound SoundManager>>
	-> 쓰레기통 안에 그대로 두자.
		정말 그대로 두기엔 신경 쓰인다... 다른 방법이 있지 않을까?
	-> 내가 먹어버리자.
		우웩... 맛없어. 다 뱉어냈다. 다른 방법이 있지 않을까?
	-> 변기에 넣고 내리자. <<if $BathRoomKey == true>>
		<<objectDelete TrashCan BloodTrash>>
		<<playSound SoundManager 21>>
		<<wait 1>>
		변기에 넣고 물을 내렸다.
		<<set $BloodTissue = 1>>
<<endif>>
===
title: CakeBox
---
냉장고 안에 케이크 상자가 들어있다.
한번 열어볼까?
-> 예.
	<<if $FingerInCakeBox == 0>>
		<<playSound SoundManager 20>>
		<<wait 0.5>>
		<<stopSound SoundManager>>
		<<ringAppear Finger>>
		케이크 상자 안에는 <color=red>잘린 손가락</color>이 들어 있었다.
		역했다. 토할 것 같았지만 애써 억눌렀다.
		이걸 변기에 넣고 내리면 하수처리장에서 걸러져 범행을 들킬 것이다.
		그럼 어떻게 하지?
		-> 휴지에 감싸서 쓰레기통에 버리자.
			쓰레기통에 버렸다.
			<<set $FingerInCakeBox = 1>>
			<<objectDelete Finger Finger>>
		-> 방법도 없으니 일단 이대로 둘까.
			다시 상자에 숨겨뒀다.
			<<ringDisappear Finger>>
		-> 음식물 쓰레기 사이에 숨겨 두자.
			여기까지 찾아보진 않겠지.
			<<objectDelete Finger Finger>>
			<<set $FingerInCakeBox = 1>>
	<<else>>
		케이크가 들어있다.
	<<endif>>
-> 아니오.
	케이크가 들어있겠지. 열지 말자.
===
title: KitchenKnife
---
평범한 식칼 치고는 조금 더 크고 날카롭다.
전문가용 같은데..., 내가 요리에 취미가 있었나?
===
title: Comic
---
만화책이 몇 권 꽂혀 있다.
===
title: Novel
---
두꺼운 소설책이 몇 권 꽂혀 있다.
===
title: BathRoomTrash
---
쓰레기가 바닥에 나뒹굴고 있다.
나: 이런 것도 안치우고 뭘 하는 거야?
===
title: Shampoo
---
샴푸가 담긴 통이다.
라벤더 향이라고 쓰여 있다.
===
title: FrontDoor
---
지금 나갈 시간은 없다.
경찰이 오기 전까지 남은 흔적을 모두 지워야 한다.
===
title: ShoeShelf
---
평범한 운동화다. 사이즈는 260이다.
===
title:Plate
---
잘 쓰지 않는 접시가 겹겹이 쌓여 있다.
===
title: LaptopAndPencilHolder
---
노트북과 연필꽂이가 있다.
연필꽂이는 텅 비어있다.
나: 하긴, 요즘 연필이나 펜 잘 안 쓰지...
노트북을 켜볼까?
-> 예.
	<<playSound SoundManager 27>>
	<<wait 0.5>>
	고장이 났는지 켜지지 않는다. 그러고 보니 수리를 맡기려 했던 것 같기도...
-> 아니오.
	이 상황에서 굳이 켤 필요는 없어.
===

title: KitchenDrawer
---
신경쓰이는 것은 없다.
===
title: RefrigeratorUp
---
텅 비어있다.
===

title: Water
---
물이 담겨있는 물병이다.
마실까?
-> 그냥 마시자.
	<<playSound SoundManager 40>>
	마침 목이 말랐는데 잘 됐다. 시원하다.
-> 됐어.
	그래. 언제 사둔 건지 기억도 안 나는데 마시지 말자.
===
title: Box
---
냉동식품의 포장지가 남아있다.
쓰레기도 똑바로 처리 못했네......
===
title: Socket1
---
냉장고 옆 콘센트다.
냉장고 선은 다른 곳에 꽂혀있는 모양이다.
===
title: Socket2
---
책상 밑 콘센트다.
나: 그러고보니 책상에 전등 하나 없네...
===
title: Mirror
---
화장대 위에는 거울이 매달려있다.
내 앉은 키보다는 높아서 살짝 불편하다.
===
title: Cloth
---
몇 안되는 옷이 걸려있다.
자취를 하다보니 정말 필요한 옷 말고는 전부 본가에 있다.
<<if $BloodOnClothInCloset == true>>
	화장실에서 발견했던 피 묻은 옷은 구석에 잘 숨겨져있다.
<<endif>>
===
title: Bed
tags:
---
내 방에 있는 침대이다.
푹신하긴 하지만, 너무 좁다.
===

title: ToInfering
---
<<fadeIn FadeImage FadeImage>>
<<wait 1>>
<<jumpScene Infering>>
===