title: variables
---
<<set $BedBloodCleanOrNot = false>>
<<set $Observation = 10>>
<<set $SinkTissueBurn = false>>
<<set $RefrigeratorEvidence = false>>
<<set $DeskEvidence = false>>
<<set $FloorHammer = true>>
<<set $BathRoomKey = false>>
<<set $StalkingPicture = true>>
<<set $LighterAndCigarette = false>>
<<set $ClueToTheTruth = true>>
<<set $BloodHandprint = true>>
<<set $SmellOfBlood = true>>
<<set $CigaretteInBathroom = false>>
<<set $BloodOnCloth = true>>
<<set $CheckTheMajor = false>>
===
title: Bed
tags:
---
내 방에 있는 침대이다.
푹신하긴 하지만, 너무 좁다.
-> 더 살펴볼래.
	<<if $BedBloodCleanOrNot == false>>
	<<jump Blood>>
	<<else>>
	특별한 점이 있진 않다.
	<<endif>>
-> 이정도면 됐어.
===
title: Blood
---
<color=red>피 묻은 자국</color>이 있다.
-> 지워야 해.
	자국을 깨끗이 지웠다.
	<<set $BedBloodCleanOrNot = true>>

-> 그대로 둬도 괜찮을 것 같아.
-> 핏자국을 확인해봐야겠어. <<if $Observation > 10>>
	이건... 피가 튀었다기보단 흐르는 피가 닦인 것 같다.
// <<if getCrimeEvidenceInStudio() == false>>
// 아직 흔적이 남았다.
// 경찰이 내가 범죄를 저질렀다는 사실을 알면 안된다.
// 더 찾아보자.
// <<else>>
// 다 지운 것 같다.
// 이제 휴대폰만 확인하면 된다.
// <<endif>>
===
title: Sink
---
싱크대이다.
-> 안쪽을 확인해볼까?
	말라붙은 귤 껍데기가 있다.
	<<if $SinkTissueBurn == false>>
	<color=red>피 묻은 휴지</color>가 있다.
	-> 태우자.
		휴지를 태웠다.
		<<set $SinkTissueBurn = true>>
	-> 신경쓰지 않아도 될거야.
	<<endif>>

-> 별로 도움될 만한 게 있을 것 같지 않아.
===
title: Refrigerator
---
내 키보다 조금 작은 냉장고이다.
겉에는 여러 전단지들이 붙어있다.
-> 뭐가 들어있지?
	냉장고 문을 열었다.
	식수와 먹다 남은 샐러드 뿐이다.
	안쪽에 케이크 상자와 같은 것이 있다.
	-> 상자를 열어볼까?
		<<if $RefrigeratorEvidence == false>>
			...케이크와 함께 무엇인지 알 수 없는 <color=red>붉은 살점</color>이 들어있다.
			-> 당장 봉지에 싸서 숨기자.
				검은 봉지에 넣어 경찰이 알 수 없을만한 곳에 숨겼다.
				<<set $RefrigeratorEvidence = true>>
			-> 신경쓰지 않아도 될거야.
				다시 냉장고 문을 닫았다.
		<<else>>
			절반 정도 남은 케이크가 들어있다.
		<<endif>>

	-> 케이크가 들어있을거야. 신경 쓸 필요 없어.
		다시 냉장고 문을 닫았다.

-> 비어있겠지. 굳이 안봐도 돼.
===
title: FrontDoor
---
현관문이다.
지금 나갈 시간은 없다.
경찰이 오기까지 남은 흔적을 모두 지워야 해.
===
title: Desk
---
공부할 때 쓰는 책상이다.
위에는 각종 교재와 노트가 늘어져있다.
앞에는 책장이 자리잡고 있다.
-> 책장엔 뭐가 있지?
	어학 시험 준비를 위한 문제집, 프로그래밍 언어 교습서 등이 있다.
	자주 읽는 경제학 책이나 자기계발서도 꽂혀있다.
	<<if $DeskEvidence == false>>
		한 책의 표면에 <color=red>붉은 손자국</color>이 있다.
		-> 당장 지워야 한다.
			자국을 깨끗이 지웠다.
			<<set $DeskEvidence = true>>
		-> 이정도면 경찰도 보지 못할 것이다.
			대충 냅뒀다.
		-> 이런게 어떻게 생긴 거지?
			아무리 봐도 어쩌다 생긴 것 같진 않다.
			누가 일부러 만들어둔 걸까?
	<<endif>>

-> 교재들만 멀쩡하면 그만이다.

===
title: ShoeShelf
---
평범한 운동화다. 사이즈는 260이다.
===
title: DresserDrawer
---

===
title: KitchenFloor
---
<<if $FloorHammer == true>>
	아까 내가 발견한 망치이다. 철 부분이 온통 피 범벅이다.
	어떻게 처리할까?
	-> 쓰레기통에 쑤셔 넣는다.
		쓰레기는 쓰레기통에!
		쓰레기통에 쑤셔 넣었다.
		<<set $FloorHammer = false>>
	-> 침대 밑에 숨긴다.
		침대 밑에 숨기면 아무도 찾지 못할 것이다.
		침대 밑에 숨기려고 이불을 들췄는데, 무언가 반짝이는 것이 보였다. 
		화장실 문 열쇠를 발견했다.
		<<set $FloorHammer = false>>
		<<set $BathRoomKey = true>>
	-> 이대로 둔다.
		아직은 처리 방법이 떠오르지 않는다. 일단 이대로 내버려 두자.
<<else>>
	신경쓰이는 것은 없다.
<<endif>>
===
title: KitchenKnife
---
평범한 식칼이다. 날카롭다.
===
title: Window
---
<<if $BloodHandprint == true>>
	창문에 손모양의 핏자국이 있다.
	일단 닦아낼까?
	-> 당연하지!
		생각보다 높이 있어 닦기가 어려웠다.
		침대 위에 올라가 핏자국을 닦아냈다.
		<<set $BloodHandprint = false>>
	-> 굳이?
		그래. 그냥 두자. 어차피 블라인드 내리면 안 보이겠지.
<<else>>
	하늘이 온통 흩뿌옇다.
	비가 올지도 모르겠다.
<<endif>>
===
title: StalkingPicture
---
<<if $StalkingPicture == true>>
	스토킹의 흔적이다. B군의 사진이 잔뜩 붙어있다.
	어떻게 할까?
	-> 떼어내어 정리한 후 옷장 속 옷 주머니 안에 숨긴다.
		옷장 속 옷 주머니 안에 사진을 넣으려다 무언가를 발견했다. 
		라이터와 담배가 들어있다. 
		<<set $StalkingPicture = false>> //스토킹 사진 옷 속에 숨김
		<<set $LighterAndCigarette = true>> //라이터와 담배 발견
		이걸로 무엇을 할까?
		-> 라이터로 사진을 태운다.
			라이터를 킨 뒤 사진을 라이터에 갖다 댔다.
			타는 냄새와 함께 사진이 형체도 남기지 않고 사라졌다.
			라이터를 다시 켜보려 하니 그게 마지막 기름이었는지 더 이상 켜지지 않는다.
			<<set $LighterAndCigarette = false>> //라이터 망가짐
		-> 일단 둘 다 챙긴다.
			언제 쓸 지 모르니 둘 다 주머니에 챙겨두었다.
		-> 담배는 백해무익! 둘 다 버린다.
			기억을 잃어버린 김에 담배도 끊기로 결심했다. 
			담배와 라이터를 모두 쓰레기통에 버렸다. 
			건강하게 살자!
			<<set $LighterAndCigarette = false>> //라이터와 담배 모두 버림
	-> 떼어내어 책 속에 숨긴다.
		제일 두꺼운 책을 꺼내 그 안에 사진을 넣었다.
		<<set $StalkingPicture = false>> //스토커 사진 책 속에 숨김
	-> 경찰이 들어오면 우리만의 추억이라고 우긴다.
		그냥 이대로 두고 추억이라고 우겨야겠다. 추억은 맞잖아?
<<else>>
	테이프 자국도 없이 깨끗하다.
<<endif>>
===
title: BookShelf
---
아무래도 전공 교재인 듯하다. 
순서대로 표지에 <color=yellow>‘인지심리학, 자료구조, 데이터베이스 개론과 실습, 사회심리학의 이해’</color>라고 적혀있다.
<<if $CheckTheMajor == false>>
	그런데 내 전공이… 뭐였더라?
	-> 심리학과 학생이다.
		그렇다면 이 <color=yellow>‘자료구조’</color>와 <color=yellow>‘데이터베이스 개론과 실습’</color>의 주인은 내가 아니겠군.
		<<set $ClueToTheTruth = false>>
	-> 컴퓨터공학과 학생이다.
		그렇다면 이 <color=yellow>‘인지심리학’</color>과 <color=yellow>‘사회심리학’</color>의 주인은 내가 아니겠군. 누가 주인일까?
		-> B군이 주인이다.
			B군은 나와 다른 과였지. 내가 B군을 스토킹하다가 훔친 모양이네.
		-> C양이 주인이다.
			C양의 책이 왜 여기에…? C양과 나는 친한 사이였던 걸까?
			<<set $ClueToTheTruth = true>>
		-> E양이 주인이다.
			E양은… 나와 같은 과 아니었나?
			<<set $ClueToTheTruth = false>>
	-> 심리학과와 컴퓨터공학과를 복수전공 중이다. 
		그렇다면 이 책들 모두 내 것이겠군.
		<<set $ClueToTheTruth = false>>
	<<set $CheckTheMajor = true>>
<<endif>>
===
title: BathRoom
---
화장실 문을 벌컥 열고 들어섰다. 
<<if $SmellOfBlood == true>>
	희미한 쇠 냄새가 감돈다.
	이 희미한 쇠 냄새는 아마도 피 냄새겠지…
	찜찜하다. 냄새를 어떻게 지울까?
	-> 방 창문을 열고 환기시킨다.
		창문과 문을 모두 열고 환기를 시작했다.
		큰 효과는 없었다.
		결국 창문을 다시 닫았다.
	-> 포기한다.
		냄새가 좀 나지만… 별 수 없다. 이대로 두자.
	
	-> 담배를 피워 냄새를 없앤다. <<if $LighterAndCigarette == true>>
		담배에 불을 붙였다. 
		독한 담배향 덕분에 냄새가 금방 지워진다. 
		라이터의 기름이 다 떨어졌는지 라이터가 더 이상 켜지지 않는다.
		<<set $LighterAndCigarette = false>>
		<<set $SmellOfBlood = false>>
		<<set $CigaretteInBathroom = true>>	
<<else>>
	이제 별다른 냄새가 나지 않는다.
<<endif>>
===
title: Basin
---
작은 세면대이다.
<<if $BloodOnCloth == true>>
	아무래도 쇠냄새의 원인을 찾은 것 같다. 
	세면대 안에 피에 젖어 검게 변하고 있는 옷이 있다.
	옷을 뒤적거리는데 주머니에서 무언가 나온다. 
	이건... <color=orange>반지</color>? 
	<<wait 1>>
	주인을 알 수 없는 반지를 주웠다.
	은색에 검은 진주가 박힌 예쁜 반지다.
	피 묻은 옷은 어디에 숨겨둘까? 
	-> 옷장 안에 숨긴다.
		잘 숨겨뒀다.
	-> 부엌 서랍 안에 숨긴다.
		잘 숨겨뒀다.
	-> 침대 밑에 숨긴다.
		잘 숨겨뒀다.
<<endif>>
===